buildscript { 
  apply from: 'dependencies.gradle'

  repositories {
    google()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath paths.gradle.android
    classpath paths.gradle.fabric
    classpath paths.gradle.google_services
    classpath paths.gradle.kotlin

    // Temporary workaround, see https://issuetracker.google.com/115738511
    classpath 'com.android.tools.build.jetifier:jetifier-processor:1.0.0-beta02'
  }
}

plugins {
  id "com.diffplug.gradle.spotless" version "3.16.0"
  id 'com.github.ben-manes.versions' version "0.20.0"
}

allprojects {
  // increase maximum number of printed errors
  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xmaxerrs" << "1000"
    }
  }
}

subprojects {
  apply plugin: 'com.diffplug.gradle.spotless'
  spotless {
    kotlin {
      target "**/*.kt"
      ktlint(versions.ktlint).userData(['indent_size': '2'])
      licenseHeaderFile file('../spotless/copyright.kt')
      endWithNewline()
    }
  }
}